/*
MIT License

Copyright (C) 2018 by Marijn Haverbeke <marijn@haverbeke.berlin> and others

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
*/

@top Grammar { declaration* }

@skip { whitespace | LineComment | BlockComment }

@precedence {
  call
  namespace
  inline
  repeat

  scopedSkip
}

declaration[@isGroup=Declaration] {
  RuleDeclaration |
  topRuleDeclaration |
  PrecedenceDeclaration {
    at<"@precedence"> PrecedenceBody {
      "{" (Precedence { PrecedenceName (at<"@left"> | at<"@right"> | at<"@cut">)? } ","?)* "}"
    }
  } |
  TokensDeclaration {
    at<"@tokens"> TokensBody { "{" tokenDeclaration* "}" }
  } |
  LocalTokensDeclaration {
    at<"@local"> kw<"tokens"> TokensBody { "{" (tokenDeclaration | ElseToken { at<"@else"> RuleName Props? })* "}" }
  } |
  ExternalTokensDeclaration {
    at<"@external"> kw<"tokens"> Name kw<"from"> Literal externalTokenSet
  } |
  ExternalPropDeclaration {
    at<"@external"> kw<"prop"> Name (kw<"as"> Name)? kw<"from"> Literal
  } |
  ExternalPropSourceDeclaration {
    at<"@external"> kw<"propSource"> Name kw<"from"> Literal
  } |
  ExternalSpecializeDeclaration {
    at<"@external"> (kw<"extend"> | kw<"specialize">) Body Name kw<"from"> Literal externalTokenSet
  } |
  ContextDeclaration {
    at<"@context"> Name kw<"from"> Literal
  } |
  DialectsDeclaration {
    at<"@dialects"> DialectBody { "{" (Name ","?)* "}" }
  } |
  TopSkipDeclaration {
    at<"@skip"> Body
  } |
  SkipScope {
    at<"@skip"> Body !scopedSkip SkipBody { "{" (RuleDeclaration | topRuleDeclaration)* "}" }
  } |
  DetectDelimDeclaration { at<"@detectDelim"> }
}

externalTokenSet[@name=TokensBody] {
  "{" (Token { RuleName Props? } ","?)* "}"
}

tokenDeclaration[@isGroup=Declaration] {
  TokenPrecedenceDeclaration {
    at<"@precedence"> PrecedenceBody { "{" ((Literal | nameExpression) ","?)* "}" }
  } |
  TokenConflictDeclaration {
    at<"@conflict"> ConflictBody { "{" (Literal | nameExpression) ","? (Literal | nameExpression) "}" }
  } |
  LiteralTokenDeclaration {
    Literal Props?
  } |
  RuleDeclaration
}

RuleDeclaration { RuleName Props? ParamList? Body }

topRuleDeclaration[@name=RuleDeclaration] { at<"@top"> RuleName Props? ParamList? Body }

ParamList { "<" (Name ("," Name)*)? ">" }

Body { "{" expression? "}" }

Props { "[" ((Prop ",")* Prop)? "]" }

Prop { (AtName | Name) ("=" (Literal | Name | "." | PropEsc { "{" RuleName "}" })*)? }

expression {
  seqExpression |
  Choice[group=Expression] { seqExpression? ("|" seqExpression?)+ }
}

seqExpression {
  atomExpression |
  Sequence[group=Expression] {
    marker (atomExpression | marker)* |
    atomExpression (atomExpression | marker)+
  }
}

atomExpression[@isGroup=Expression] {
  Literal |
  CharSet |
  AnyChar |
  InvertedCharSet |
  nameExpression |
  CharClass {
    @specialize<AtName, "@asciiLetter" | "@asciiUpperCase" | "@asciiLowerCase" | "@digit" | "@whitespace" | "@eof">
  } |
  Optional { atomExpression !repeat "?" } |
  Repeat { atomExpression !repeat "*" } |
  Repeat1 { atomExpression !repeat "+" } |
  InlineRule { (RuleName !inline Props? | Props) Body } |
  ParenExpression { "(" expression? ")" } |
  Specialization { (at<"@specialize"> | at<"@extend">) Props? ArgList }
}

nameExpression {
  RuleName |
  ScopedName |
  Call { (RuleName | ScopedName) !call ArgList }
}

marker {
  PrecedenceMarker { "!" PrecedenceName } |
  AmbiguityMarker { "~" Name }
}

ScopedName { RuleName !namespace "." RuleName }

ArgList {
  "<" (expression ("," expression)*)? ">"
}

RuleName { name }

PrecedenceName { name }

Name { name }

kw<value> { @specialize[@name={value}]<keyword, value> }

at<value> { @specialize[@name={value}]<AtName, value> }

@tokens {
  whitespace { @whitespace+ }

  LineComment { "//" ![\n]* }

  BlockComment { "/*" blockCommentRest }

  blockCommentRest { ![*] blockCommentRest | "*" blockCommentAfterStar }

  blockCommentAfterStar { "/" | "*" blockCommentAfterStar | ![/*] blockCommentRest }

  name { (@asciiLetter | @digit | $[\-_\u{a1}-\u{10ffff}])+ }

  AnyChar { "_" }

  @precedence { AnyChar, whitespace, name }

  keyword { name }

  @precedence { whitespace, keyword }

  AtName { "@" name }

  Literal {
    '"' (![\\\n"] | "\\" _)* '"'? |
    "'" (![\\\n'] | "\\" _)* "'"?
  }

  CharSet { "$[" (![\\\]] | "\\" _)* "]" }
  InvertedCharSet { "![" (![\\\]] | "\\" _)* "]" }

  @precedence { InvertedCharSet, "!" }

  "{" "}" "(" ")" "[" "]"
  "=" "." "|" "!" "~" "*" "+" "?"
}

// @external propSource lezerHighlighting from "./highlight"

@detectDelim
